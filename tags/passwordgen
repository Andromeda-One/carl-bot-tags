{=(COMMENT):#### Generates random passwords for you. There are different strength options to suit your needs. ####}
{=(COMMENT):Change the prefix below to your server's prefix.}
{=(prefix):-}

{=(COMMENT):The user's input stored here.}
{=():}
{=(l1):{lower:{1}}}
 
{=(COMMENT):Generates the random chars here.}
{=(letter):#:a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z}
{=(number):range:1-9}
{=(symbol):#:!,@,#,$,%,^,&,+,=,-}

{=(COMMENT):Different password strengths generated here.}
{=(wmrandchar):#:letter,number}
{=(srandchar):#:letter,number,symbol}
{=(wmchar):#:{{wmrandchar}},{{wmrandchar}},{{wmrandchar}}}
{=(schar):#:{{srandchar}},{{srandchar}},{{srandchar}}}
 
{=(wpassword):{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}}
 
{=(mpassword):{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}{{{{wmchar}}}}}
 
{=(spassword):{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}{{{{schar}}}}}
 
{=(passtype):{if({l1}==weak):weak|{if({l1}==medium):medium-strength|{if({l1}==strong):strong}}}}
 
{override}

{=(COMMENT):Sends your password to you.}
{{or({l1}!=|{l1}==weak|{l1}==medium|{l1}==strong):c:cembed {channel(id)} {"description":"⚡ {user(mention)}, encoded cosmic rays have been beamed directly to your Direct Message inbox (your {passtype} password has been sent in DMs)!","color":3092790}}}
{dm}

{=(COMMENT):Helps the user figure out the tag.}
{=(helpmsg):{if({l1}==):Use `{prefix}password weak`, `{prefix}password medium`, or `{prefix}password strong` to get a completely random password! Here's some stats: ```• Weak Password - 7 characters, capital and lowercase letters, numbers (1-9)
 
• Medium Password - 10 characters, capital and lowercase letters, numbers (1-9)
 
• Strong Password - 12 characters, capital and lowercase letters, numbers (1-9), symbols (!@#$%^&+=-)```}}

{=(COMMENT):General things for the embed generation.}
{=(title):{if({l1}!=):Personal Password}}
{=(thumbnail):{if({l1}!=):https://i.imgur.com/6YFCmQh.png}}
{=(footer):{if({l1}!=):Do not share this password with anyone if you decide to use it! Save these messages in case you forget your password.}}

{=(error):{and({l1}!=|{l1}!=weak|{l1}!=medium|{l1}!=strong):text}}

{=(COMMENT):Weak password message that you get.}
{=(wpassmsg):{if({l1}==weak):The password below contains **7 randomly-generated characters**, including:
• Lower and/or upper alphabetical characters (A-Z)
• Decimal numbers (0-9)

__Open the spoiler for your password__
↓↓↓↓↓↓↓
||`{wpassword}`||
There's always a chance for it to send *only* numbers, or *only* letters. If this happens, feel free to generate another password.}}

{=(COMMENT):Medium password message.}
{=(mpassmsg):{if({l1}==medium):The password below contains **10 randomly-generated characters**, including:
• Lower and/or upper alphabetical characters (A-Z)
• Decimal numbers (0-9)

__Open the spoiler for your password__
↓↓↓↓↓↓↓↓↓↓
||`{mpassword}`||
There's always a chance for it to send *only* numbers, or *only* letters. If this happens, feel free to generate another password.}}

{=(COMMENT):Finally, the strong password message.}
{=(spassmsg):{if({l1}==strong):The password below contains **12 randomly-generated characters**, including:
• Lower and/or upper alphabetical characters (A-Z)
• Decimal numbers (0-9)
• Special characters/Symbols

__Open the spoiler for your password__
↓↓↓↓↓↓↓↓↓↓↓↓
||`{spassword}`||
There's always a chance for it to send *only* numbers, *only* letters, or *only* symbols. If this happens, feel free to generate another password.}}

{=(COMMENT):Final output.}
{c:cembed {channel(id)} {"url":"https://en.wikipedia.org/wiki/Password","title":"{title}","description":"{helpmsg} {wpassmsg} {mpassmsg} {spassmsg}","thumbnail":{"url":"{thumbnail}"},"image":{"url":"{error}"},"color":3092790,"footer":{"text":"{footer}"}}}
